import main.compiler
import main.ir
import main.bytecode
import main.llvm
import main.ast
import main.prelude

adt ParamToValue
	Nil
	Cons(param:IRParam, value:pointer, tail:ParamToValue)
end

adt VarToValue
	Nil
	Cons(var_:Variable, value:pointer, tail:VarToValue)
end

adt LatePHIList
	Nil
	Cons(llvmPhi:pointer, phi:PHIList, tail:LatePHIList)
end

adt LLVMCodegen
	New
	
	field context:CompilerContext
	field file:IRSourceFile
	
	field module:pointer
	field llvmFun:pointer
	field builder:pointer
	
	field params:ParamToValue
	field vars:VarToValue
	
	field latePhi:LatePHIList
	
	field adts:LLVMADTList
	field di:pointer
	
	field diCU:pointer
end

adt LLVMADT
	New
	
	field adt_:IRADTDecl
	field type:pointer
	field opaque:bool
	
	field cons:LLVMADTConsList
end

adt LLVMADTList
	Nil
	Cons(adt_:IRADTDecl, llvm:LLVMADT, tail:LLVMADTList)
end

adt LLVMADTCons
	New
	
	field cons:LLVMADTConsList
	field type:pointer
end

adt LLVMADTConsList
	Nil
	Cons(cons:IRADTCons, llvm:LLVMADTCons, tail:LLVMADTConsList)
end

adt IntrinsicInfo
	New
	
	field name:string
	field ret:pointer
	field params:pointer[]
	field paramsCount:i32
end

global llvm:pointer = LLVMContextCreate()

global llvmI1Type:pointer = LLVMInt1TypeInContext(llvm)
global llvmPtrType:pointer = LLVMPointerType(LLVMInt8TypeInContext(llvm), 0)
global llvmStringType:pointer = LLVMPointerType(LLVMInt8TypeInContext(llvm), 0)
global llvmI32Type:pointer = LLVMInt32TypeInContext(llvm)

global llvmNullPtrList:pointer[] = new pointer[1]

global IntrinsicStringConcat:IntrinsicInfo = IntrinsicInfoCreate2("jf_string_concat", 2, llvmStringType, llvmStringType, llvmStringType)
global IntrinsicStringCmp:IntrinsicInfo = IntrinsicInfoCreate2("jf_string_compare", 2, llvmI32Type, llvmStringType, llvmStringType)
global IntrinsicAlloc:IntrinsicInfo = IntrinsicInfoCreate1("jf_allocate", 1, llvmPtrType, llvmI32Type)



function IntrinsicInfoCreate(name:string, count:i32, ret:pointer):IntrinsicInfo
	var info = IntrinsicInfo.New
	info.name = name
	info.ret = ret
	info.params = new pointer[count]
	info.paramsCount = count
	return info
end

function IntrinsicInfoCreate2(name:string, count:i32, ret:pointer, p1:pointer, p2:pointer):IntrinsicInfo
	var info = IntrinsicInfoCreate(name, count, ret)
	info.params[0] = p1
	info.params[1] = p2
	return info
end

function IntrinsicInfoCreate1(name:string, count:i32, ret:pointer, p1:pointer):IntrinsicInfo
	var info = IntrinsicInfoCreate(name, count, ret)
	info.params[0] = p1
	return info
end


function RunLLVMCodegen(context:CompilerContext)
	LLVMCodegenUnits(context, context.units)
end

function LLVMCodegenUnits(context:CompilerContext, units:IRCompilationUnitList)
	var units = context.units
	while true do
		match units
		case Nil 
			return
		case Cons(unit_, tail)
			LLVMCodegenUnit(context, unit_)
			units = tail
		end
	end
end

function LLVMCodegenUnit(context:CompilerContext, unit_:IRCompilationUnit)
	var files = unit_.files
	while true do
		match files
		case Nil
			return
		case Cons(file, tail)
			LLVMCodegenFile(context, file)
			files = tail			
		end
	end
end

function LLVMCodegenFile(context:CompilerContext, file:IRSourceFile)
	var codegen = LLVMCodegenCreate(context, file)
	
	var fullName = file.unit_.name
	if file.file.suffix <> "" then
		fullName = fullName + "@" + file.file.suffix
	end
	
	codegen.module = LLVMModuleCreateWithNameInContext(fullName, llvm)
	codegen.di = LLVMCreateDIBuilder(codegen.module, true)
	
	var diFile = LLVMDICreateFile(codegen.di, file.file.filename, file.file.dir, 0, "")
	
	codegen.diCU = LLVMDICreateCompileUnit(codegen.di, 2, diFile, "Jellyfish Compiler", false, "", 0, "", 1, 0, false, false)
	
	LLVMCodegenSymbols(codegen, file.symbols)

	
	var moduleString = LLVMPrintModuleToString(codegen.module)
	var filePath = context.outputPath + "/ll-bitcode/" + fullName + ".ll"
	LLVMWriteBitcodeToFile(codegen.module, filePath)
	
	filePath = context.outputPath + "/ll-ir/" + fullName + ".ll"
	WriteFile(filePath, moduleString)
end


function LLVMCodegenCreate(context:CompilerContext, file:IRSourceFile):LLVMCodegen
	var codegen:LLVMCodegen = LLVMCodegen.New
	codegen.context = context
	codegen.file = file
	codegen.adts = LLVMADTList.Nil
	
	var builder = LLVMCreateBuilderInContext(llvm)
	codegen.builder = builder
	
	return codegen
end

function LLVMCodegenSymbols(codegen:LLVMCodegen, symbols:IRSymbolList)
	while true do
		match symbols
		case Nil return
		case Cons(symbol, tail)
			LLVMCodegenSymbol(codegen, symbol)
			symbols = tail
		end
	end
end

function LLVMCodegenError(codegen:LLVMCodegen, loc:SrcLoc, message:string):undef
	match loc
	case At(line, col)
		WriteLn(codegen.file.file.path + "(line " + IntToStr(line + 1) + ", col " + IntToStr(col + 1) + "): LLVMCodegen Error: " + message)
	end
	return Exit(1)
end

function LLVMCodegenSymbol(codegen:LLVMCodegen, symbol:IRSymbol)
	match symbol
	case ADT(adt_)
		LLVMCodegenADTDeclFull(codegen, adt_)
		LLVMCodegenADTConsDecls(codegen, adt_.cons)
	case Global(global_)
		LLVMCodegenGlobal(codegen, global_)
	case Function(fun)
		LLVMCodegenFunction(codegen, fun)
	end
end

function MangleUnitName(unit_:IRCompilationUnit):string
	return unit_.name
end

function MangleGlobalName(global_:IRGlobalDecl):string
	return "g$" + MangleUnitName(global_.symbol.owner.unit_) + "." + global_.symbol.name
end

function MangleGlobalGetterName(global_:IRGlobalDecl):string
	return "gg$" + MangleUnitName(global_.symbol.owner.unit_) + "." + global_.symbol.name
end

function MangleGlobalInitName(global_:IRGlobalDecl):string
	return "gi$" + MangleUnitName(global_.symbol.owner.unit_) + "." + global_.symbol.name
end

function MangleADTName(adt_:IRADTDecl):string
	return "adt$" + MangleUnitName(adt_.symbol.owner.unit_) + "." + adt_.symbol.name
end

function MangleADTConsName(constr:IRADTCons):string
	return "adtc$" + MangleUnitName(constr.owner.symbol.owner.unit_) + "." + constr.owner.symbol.name + "." + constr.name
end

function MangleADTConsFunctionName(constr:IRADTCons):string
	return "adtcfn$" + MangleADTConsName(constr)
end

function MangleFunctionName(fun:IRFunctionDecl):string
	if fun.symbol.owner.unit_.name == "main.main" and fun.symbol.name == "run" then
		return "_jf_entry_point"
	end
	
	return "fn$" + MangleUnitName(fun.symbol.owner.unit_) + "." + fun.symbol.name
end

function LLVMCodegenGlobal(codegen:LLVMCodegen, global_:IRGlobalDecl)
	var llvmDecl = LLVMCodegenGlobalDecl(codegen, global_)
	LLVMSetInitializer(llvmDecl, LLVMGetUndef(LLVMGetElementType(LLVMTypeOf(llvmDecl))))
	
	var llvmFun = LLVMCodegenGlobalGetterDecl(codegen, global_)	
	
	var guard = LLVMAddGlobal(codegen.module, llvmI1Type, "")
	LLVMSetLinkage(guard, LLVMPrivateLinkage())
	LLVMSetInitializer(guard, LLVMConstInt_wrap(llvmI1Type, 0, true))
	
	var builder = codegen.builder
	var bb = LLVMAppendBasicBlockInContext(llvm, llvmFun, "entry")
	var retBlock = LLVMAppendBasicBlockInContext(llvm, llvmFun, "ret")
	var initBlock = LLVMAppendBasicBlockInContext(llvm, llvmFun, "init")
	LLVMPositionBuilderAtEnd(builder, bb)
	var guardVal = LLVMBuildLoad(builder, guard, "")
	LLVMBuildCondBr(builder, guardVal, retBlock, initBlock)
	
	LLVMPositionBuilderAtEnd(builder, initBlock)
	LLVMBuildStore(builder, LLVMConstInt_wrap(llvmI1Type, 1, true), guard)
	var initVal = LLVMBuildCall(builder, LLVMCodegenGlobalInitDecl(codegen, global_), llvmNullPtrList, 0, "")
	LLVMBuildStore(builder, initVal, llvmDecl)
	LLVMBuildRetVoid(builder)
	
	LLVMPositionBuilderAtEnd(builder, retBlock)
	LLVMBuildRetVoid(builder)	
	
	llvmFun = LLVMCodegenGlobalInitDecl(codegen, global_)
	LLVMCodegenClearState(codegen)
	codegen.llvmFun = llvmFun
	bb = LLVMAppendBasicBlockInContext(llvm, llvmFun, "entry")
	LLVMPositionBuilderAtEnd(builder, bb)
	LLVMCodegenFunctionFrame(codegen, global_.expr.frame)
end

function LLVMCodegenGlobalDecl(codegen:LLVMCodegen, global_:IRGlobalDecl):pointer
	var gval:pointer
	var name = MangleGlobalName(global_)
	
	gval = LLVMGetNamedGlobal(codegen.module, name)
	if gval == Null() then
		var type = LLVMCodegenType(codegen, global_.type.loc, global_.type.type)		
		gval = LLVMAddGlobal(codegen.module, type, name)
	end
		
	return gval
end

function LLVMCodegenGlobalGetterDecl(codegen:LLVMCodegen, global_:IRGlobalDecl):pointer
	var name = MangleGlobalGetterName(global_)
	
	return LLVMCreateFunctionSignature(codegen, name, IRParamList.Nil, global_.type.loc, IRType.Prim(IRPrimType.Unit))
end

function LLVMCodegenGlobalInitDecl(codegen:LLVMCodegen, global_:IRGlobalDecl):pointer
	var name = MangleGlobalInitName(global_)
	
	return LLVMCreateFunctionSignature(codegen, name, IRParamList.Nil, global_.type.loc, global_.type.type)
end

function LLVMCodegenType(codegen:LLVMCodegen, loc:SrcLoc, type:IRType):pointer
	match type
	case Ref(ref)
		LLVMCodegenError(codegen, loc, "ref types must not appear in the codegen")
	case Prim(prim)
		match prim
		case Unit return LLVMVoidTypeInContext(llvm)
		case I32 return LLVMInt32TypeInContext(llvm)
		case String return llvmStringType
		case Pointer return LLVMPointerType(LLVMInt8TypeInContext(llvm), 0)
		case Bool return LLVMInt1TypeInContext(llvm)
		case Undef return LLVMVoidTypeInContext(llvm)
		end
	case ADT(adt_)
		return LLVMPointerType(LLVMCodegenADTDeclOpaque(codegen, adt_), 0)
	case ADTMetaclass(decl)
		LLVMCodegenError(codegen, loc, "meta types must not appear in the codegen")
	case ArrayUnsized(elementType)
		var elTy = LLVMCodegenType(codegen, loc, elementType)
		return LLVMPointerType(elTy, 0)
	case ArraySized(elementType, size)
		LLVMCodegenError(codegen, loc, "sized array type should not appear in a compiled bytecode")
	case other
		LLVMCodegenError(codegen, loc, "type not implemented")
	end
end

function count_params(params:IRParamList):i32
	var i = 0
	while true do
		match params
		case Nil return i
		case Cons(param, tail)
			params = tail
			i = i + 1
		end
	end
end

function LLVMGetADTMapping(codegen:LLVMCodegen, adt_:IRADTDecl):LLVMADT
	var list = codegen.adts
	while true do
		match list
		case Nil 
			var llvmADT = LLVMADT.New
			llvmADT.adt_ = adt_
			llvmADT.type = Null()
			llvmADT.cons = LLVMADTConsList.Nil
			codegen.adts = LLVMADTList.Cons(adt_, llvmADT, codegen.adts)
			return llvmADT
		case Cons(adt_1, llvmADT, tail)
			if adt_1 == adt_ then
				return llvmADT
			else
				list = tail
			end
		end
	end
end

function LLVMADTGetConsMapping(codegen:LLVMCodegen, llvmADT:LLVMADT, cons:IRADTCons):LLVMADTCons
	var list = llvmADT.cons
	while true do
		match list
		case Nil 
			var llvmCons = LLVMADTCons.New
			llvmCons.type = Null()
			llvmADT.cons = LLVMADTConsList.Cons(cons, llvmCons, llvmADT.cons)
			return llvmCons
		case Cons(cons1, llvmCons, tail)
			if cons1 == cons then
				return llvmCons
			else
				list = tail
			end
		end
	end
end

function count_fields(fields:IRADTFieldList):i32
	match fields
	case Nil return 0
	case Cons(field_, tail) return 1 + count_fields(tail)
	end
end

function LLVMCodegenADTFieldTypes(codegen:LLVMCodegen, elements:pointer[], idx:i32, fields:IRADTFieldList)
	match fields
	case Nil return
	case Cons(field_, tail)
		elements[idx] = LLVMCodegenType(codegen, field_.type.loc, field_.type.type)
		LLVMCodegenADTFieldTypes(codegen, elements, idx + 1, tail)
	end
end

function LLVMCodegenParamTypes(codegen:LLVMCodegen, elements:pointer[], idx:i32, params:IRParamList)
	match params
	case Nil return
	case Cons(param, tail)
		elements[idx] = LLVMCodegenType(codegen, param.type.loc, param.type.type)
		LLVMCodegenParamTypes(codegen, elements, idx + 1, tail)
	end
end

function LLVMCodegenADTConsDecl(codegen:LLVMCodegen, cons:IRADTCons):pointer
	var llvmADT = LLVMGetADTMapping(codegen, cons.owner)
	var llvmCons = LLVMADTGetConsMapping(codegen, llvmADT, cons)
	if llvmCons.type == Null() then
		llvmCons.type = LLVMStructCreateNamed(llvm, MangleADTConsName(cons))
		
		var adt_ = cons.owner
		
		var tagCount = 1
		var fieldsCount = count_fields(adt_.fields)
		var paramsCount = count_params(cons.params)
		
		var elements:pointer[] = new pointer[tagCount + fieldsCount + paramsCount]
		elements[0] = llvmI32Type
		
		LLVMCodegenADTFieldTypes(codegen, elements, 1, adt_.fields)	
		LLVMCodegenParamTypes(codegen, elements, tagCount + fieldsCount, cons.params)
		LLVMStructSetBody(llvmCons.type, elements, tagCount + fieldsCount + paramsCount, false)
	end
	
	return llvmCons.type
end

function LLVMCodegenADTConsDecls(codegen:LLVMCodegen, list:IRADTConsList)
	while true do
		match list
		case Nil return
		case Cons(cons, tail)
			LLVMCodegenADTConsDecl(codegen, cons)
			LLVMCodegenADTConsBody(codegen, cons)
			list = tail
		end
	end
end

function LLVMCodegenADTDeclOpaque(codegen:LLVMCodegen, adt_:IRADTDecl):pointer
	var llvmADT = LLVMGetADTMapping(codegen, adt_)
	if llvmADT.type == Null() then
		llvmADT.type = LLVMStructCreateNamed(llvm, MangleADTName(adt_))
		llvmADT.opaque = true
	end
	
	return llvmADT.type
end

function LLVMCodegenADTDeclFull(codegen:LLVMCodegen, adt_:IRADTDecl):pointer
	var llvmADT = LLVMGetADTMapping(codegen, adt_)
	LLVMCodegenADTDeclOpaque(codegen, adt_)
	if llvmADT.opaque then
		llvmADT.opaque = false
		
		--tag (i32)
		--fields
		var tagCount = 1
		var fieldsCount = count_fields(adt_.fields)
		
		var elements:pointer[] = new pointer[tagCount + fieldsCount]
		elements[0] = llvmI32Type
		
		LLVMCodegenADTFieldTypes(codegen, elements, 1, adt_.fields)	
		LLVMStructSetBody(llvmADT.type, elements, tagCount + fieldsCount, false)
	end	
	return llvmADT.type
end

function LLVMCodegenADTConsBody(codegen:LLVMCodegen, cons:IRADTCons)
	var llvmFun = LLVMCodegenConsFunctionDecl(codegen, cons)
	var llvmCons = LLVMCodegenADTConsDecl(codegen, cons)
	
	var bb = LLVMAppendBasicBlockInContext(llvm, llvmFun, "entry")
	
	LLVMPositionBuilderAtEnd(codegen.builder, bb)
	
	var elements:pointer[] = new pointer[1]
	elements[0] = LLVMConstTrunc(LLVMSizeOf(llvmCons), llvmI32Type)
	
	var alloc = LLVMBuildCall(codegen.builder, LLVMCodegenGetIntrinsic(codegen, IntrinsicAlloc), elements, 1, "")
	var obj = LLVMBuildBitCast(codegen.builder, alloc, LLVMPointerType(llvmCons, 0), "")
	
	LLVMBuildStore(codegen.builder, LLVMConstInt_wrap(llvmI32Type, cons.index, true), LLVMBuildStructGEP(codegen.builder, obj, 0, ""))
	
	var offset = count_fields(cons.owner.fields)
	var n = count_params(cons.params)
	var i = 0
	while i < n do
		LLVMBuildStore(codegen.builder, LLVMGetParam(llvmFun, i), LLVMBuildStructGEP(codegen.builder, obj, i + offset + 1, ""))
		i = i + 1
	end
	
	LLVMBuildRet(codegen.builder, LLVMBuildBitCast(codegen.builder, obj, LLVMPointerType(LLVMCodegenADTDeclOpaque(codegen, cons.owner), 0), ""))
end

function LLVMCreateFunctionSignature(codegen:LLVMCodegen, name:string, params:IRParamList, retTyLoc:SrcLoc, retTy:IRType):pointer
	if name <> "" then			
		var funPtr:pointer	
		funPtr = LLVMGetNamedFunction(codegen.module, name)
		if funPtr <> Null() then
			return funPtr
		end
	end
	
	var paramCount = count_params(params)
	var paramTypes:pointer[] = new pointer[paramCount]
	
	var i = 0
	var paramList = params
	while i < paramCount do
		match paramList
		case Cons(param, tail)
			paramList = tail
			paramTypes[i] = LLVMCodegenType(codegen, param.type.loc, param.type.type)
		end
		i = i + 1
	end
	
	var retTy = LLVMCodegenType(codegen, retTyLoc, retTy)
	
	var funTy = LLVMFunctionType(retTy, paramTypes, paramCount, false)
	
	var llvmFun = LLVMAddFunction(codegen.module, name, funTy)
	
	i = 0
	paramList = params
	while i < paramCount do
		match paramList
		case Cons(param, tail)
			paramList = tail
			var llvmParam = LLVMGetParam(llvmFun, i)
			LLVMSetValueName(llvmParam, param.name)			
		end
		i = i + 1
	end
	
	return llvmFun
end

function LLVMCodegenFunctionDecl(codegen:LLVMCodegen, fun:IRFunctionDecl):pointer
	var name = MangleFunctionName(fun)
	
	var funPtr:pointer	
	funPtr = LLVMGetNamedFunction(codegen.module, name)
	if funPtr <> Null() then
		return funPtr
	end

	return LLVMCreateFunctionSignature(codegen, name, fun.params, fun.retTy.loc, fun.retTy.type)
end

function LLVMCodegenConsFunctionDecl(codegen:LLVMCodegen, constr:IRADTCons):pointer
	var name = MangleADTConsFunctionName(constr)
	var funPtr:pointer	
	funPtr = LLVMGetNamedFunction(codegen.module, name)
	if funPtr <> Null() then
		return funPtr
	end
	
	return LLVMCreateFunctionSignature(codegen, name, constr.params, constr.owner.symbol.loc, constr.owner.selfType)
end

function LLVMCodegenGetIntrinsic(codegen:LLVMCodegen, info:IntrinsicInfo):pointer
	var name = info.name
	
	var funPtr:pointer	
	funPtr = LLVMGetNamedFunction(codegen.module, name)
	if funPtr <> Null() then
		return funPtr
	end
	
	var funTy = LLVMFunctionType(info.ret, info.params, info.paramsCount, false)
	var llvmFun = LLVMAddFunction(codegen.module, name, funTy)
	return llvmFun
end

function LLVMCodegenFunction(codegen:LLVMCodegen, fun:IRFunctionDecl)
	var llvmFun = LLVMCodegenFunctionDecl(codegen, fun)
	
	LLVMCodegenClearState(codegen)
	codegen.llvmFun = llvmFun
	LLVMCodegenCaptureParams(codegen, fun, llvmFun)
	
	var builder = codegen.builder
	var bb = LLVMAppendBasicBlockInContext(llvm, llvmFun, "entry")
	LLVMPositionBuilderAtEnd(builder, bb)
	
	
	match fun.body
	case Block(block)
		LLVMCodegenFunctionFrame(codegen, block.frame)
	case FFIImport(text)
		var argCount = count_params(fun.params)
		var argArray:pointer[] = new pointer[argCount]
		
		var i = 0
		while i < argCount do
			argArray[i] = LLVMGetParam(llvmFun, i)
			i = i + 1
		end
	
		var proxy = LLVMAddFunction(codegen.module, DecodeStr(text), LLVMGetElementType(LLVMTypeOf(llvmFun)))
		var ret = LLVMBuildCall(builder, proxy, argArray, argCount, "")
		if IsVoidType(fun.retTy.type) then
			LLVMBuildRetVoid(builder)
		else
			LLVMBuildRet(builder, ret)
		end
	end
end

function LLVMCodegenClearState(codegen:LLVMCodegen)
	codegen.vars = VarToValue.Nil
	codegen.params = ParamToValue.Nil
	codegen.latePhi = LatePHIList.Nil
end

function LLVMCodegenCaptureParams(codegen:LLVMCodegen, fun:IRFunctionDecl, llvmFun:pointer)
	var i = 0
	var params = fun.params
	while true do
		match params
		case Nil return
		case Cons(param, tail)
			params = tail
			
			var llvmParam = LLVMGetParam(llvmFun, i)
			codegen.params = ParamToValue.Cons(param, llvmParam, codegen.params)
		end
		i = i + 1
	end
end

function LLVMCodegenFunctionFrame(codegen:LLVMCodegen, frame:FunctionFrame)
	LLVMCodegenVariables(codegen, frame)
	LLVMCodegenBakeArguments(codegen, frame)
	
	LLVMCodegenPreapreBasicBlocks(codegen, frame.blocks)
	
	LLVMBuildBr(codegen.builder, frame.entry.llvmRef)
	
	LLMVCodegenBasicBlocks(codegen, frame.blocks)
	
	LLVMCodegenLatePHI(codegen)
end

function LLVMCodegenLatePHI(codegen:LLVMCodegen)
	var latePhi = codegen.latePhi
	while true do
		match latePhi
		case Nil return
		case Cons(llvmPHI, phi, tail)
			latePhi = tail
			
			var count = count_phi(phi)
			var values:pointer[] = new pointer[count]
			var bbs:pointer[] = new pointer[count]
			
			var phiList = phi
			var i = 0
			while i < count do				
				match phiList
				case Cons(block, value, tail)
					values[i] = LLVMCodegenGetValue(codegen, value)
					bbs[i] = block.llvmRef
					phiList = tail
				end
				
				i = i + 1
			end
			
			LLVMAddIncoming(llvmPHI, values, bbs, count)
		end
	end
end

function GetDbgLoc(dbg:DebugLoc):SrcLoc
	match dbg
	case At(file, loc)
		return loc
	case None
		return SrcLoc.At(0, 0)
	end
end

function GetVariableName(var_:Variable):string
	match var_
	case Var(name) return name
	case Parameter(param) return "bake." + param.name
	case other return ""
	end
end

function LLVMCodegenGetValue(codegen:LLVMCodegen, value:ValueRef):pointer
	match value
	case Instr(instr)
		return instr.llvmRef
	case ConstNum(num, type)
		return LLVMConstInt_wrap(LLVMCodegenType(codegen, SrcLoc.At(0,0), type), num, true)
	case ConstString(str, ty)
		var initializer = LLVMConstStringInContext(llvm, str, StringLength(str), false)
		var globalConstant = LLVMAddGlobal(codegen.module, LLVMTypeOf(initializer), "")
		LLVMSetLinkage(globalConstant, LLVMPrivateLinkage())
		LLVMSetInitializer(globalConstant, initializer)
		return LLVMConstBitCast(globalConstant, LLVMPointerType(LLVMInt8TypeInContext(llvm), 0))
	case ConstBool(val, type)
		var num:i32
		if val then
			num = 1
		else
			num = 0
		end
		return LLVMConstInt_wrap(LLVMCodegenType(codegen, SrcLoc.At(0,0), type), num, true)
	case other
		WriteLn("ValueRef not implemented")
		Exit(1)
	end
end

function IsVoidType(type:IRType):bool
	match type
	case Prim(prim)
		match prim
		case Undef return true
		case Unit return true
		end
	end
	return false
end

function LLVMCodegenPreapreBasicBlocks(codegen:LLVMCodegen, blocks:BasicBlockList)
	while true do
		match blocks
		case Nil return
		case Cons(block, tail)
			blocks = tail
			
			var bb = LLVMAppendBasicBlockInContext(llvm, codegen.llvmFun, "")
			block.llvmRef = bb
		end
	end
end

function LLMVCodegenBasicBlocks(codegen:LLVMCodegen, blocks:BasicBlockList)
	match blocks
	case Nil return
	case Cons(block, tail)
		LLMVCodegenBasicBlocks(codegen, tail)
		LLMVCodegenBasicBlock(codegen, block)
	end
end

function count_values(args:ValueRefList):i32
	match args
	case Nil return 0
	case Cons(arg, tail) return 1 + count_values(tail)
	end
end

function count_phi(phi:PHIList):i32
	match phi
	case Nil return 0
	case Cons(p, q, tail) return 1 + count_phi(tail)
	end
end

adt Args
	Args(count:i32, array:pointer[])
end

function LLVMCodegenPredicate(codegen:LLVMCodegen, cmp:Comparison):i32
	match cmp
	case EQ return 32 
	case NE return 33
	case LT return 40
	case LE return 41
	case GT return 38
	case GE return 39
	end
end

function count_branches(cases:ADTJumpCase):i32
	match cases
	case Nil return 0
	case Cons(cons, bb, tail) return 1 + count_branches(tail)
	end
end

function find_param_index(params:IRParamList, param:IRParam):i32
	match params
	case Nil return 0
	case Cons(p, tail)
		if p == param then
			return 0
		else
			return 1 + find_param_index(tail, param)
		end
	end
end

function LLMVCodegenBasicBlock(codegen:LLVMCodegen, bb:BasicBlock)
	LLVMPositionBuilderAtEnd(codegen.builder, bb.llvmRef)

	var i = 0
	while i < bb.size do
		var instr = bb.instructions[i]
		
		match instr
		case WriteVariable(var_, value)
			var alloca = LLVMCodegenGetVariableStorage(codegen, var_)
			var value = LLVMCodegenGetValue(codegen, value)
			
			LLVMBuildStore(codegen.builder, value, alloca)
		case ReadVariable(var_)
			var alloca = LLVMCodegenGetVariableStorage(codegen, var_)
			
			var val = LLVMBuildLoad(codegen.builder, alloca, "")
			instr.llvmRef = val
		case ReadGlobal(global_)
			var globalVar = LLVMCodegenGlobalDecl(codegen, global_)
			LLVMBuildCall(codegen.builder, LLVMCodegenGlobalGetterDecl(codegen, global_), llvmNullPtrList, 0, "")
			var val = LLVMBuildLoad(codegen.builder, globalVar, "")
			instr.llvmRef = val
		case WriteGlobal(global_, value)
			var globalVar = LLVMCodegenGlobalDecl(codegen, global_)
			LLVMBuildCall(codegen.builder, LLVMCodegenGlobalGetterDecl(codegen, global_), llvmNullPtrList, 0, "")
			LLVMBuildStore(codegen.builder, LLVMCodegenGetValue(codegen, value), globalVar)
		case Unreachable
			LLVMBuildUnreachable(codegen.builder)
			return
		case Return(value)
			var type = ValueRefGetType(value)
			if IsVoidType(type) then
				LLVMBuildRetVoid(codegen.builder)
			else
				var value = LLVMCodegenGetValue(codegen, value)
				
				LLVMBuildRet(codegen.builder, value)
			end
			return
		case Jump(bb_ref)
			LLVMBuildBr(codegen.builder, bb_ref.llvmRef)
			return
		case CondJump(value, bbTrue, bbFalse)
			var cond = LLVMCodegenGetValue(codegen, value)
			LLVMBuildCondBr(codegen.builder, cond, bbTrue.llvmRef, bbFalse.llvmRef)
			return
		case PHI(phi)			
			var llvmPHI = LLVMBuildPhi(codegen.builder, LLVMCodegenType(codegen, GetDbgLoc(instr.dbg), instr.type), "")
			
			codegen.latePhi = LatePHIList.Cons(llvmPHI, phi, codegen.latePhi)
			
			instr.llvmRef = llvmPHI
		case ArrayRead(array, idx)
			var ptr = LLVMCodegenGetValue(codegen, array)
			var indices:pointer[] = new pointer[1]
			indices[0] = LLVMCodegenGetValue(codegen, idx)
			var gep = LLVMBuildInBoundsGEP(codegen.builder, ptr, indices, 1, "")
			instr.llvmRef = LLVMBuildLoad(codegen.builder, gep, "")
		case ArrayWrite(array, idx, val)
			var ptr = LLVMCodegenGetValue(codegen, array)
			var indices:pointer[] = new pointer[1]
			indices[0] = LLVMCodegenGetValue(codegen, idx)
			var gep = LLVMBuildInBoundsGEP(codegen.builder, ptr, indices, 1, "")
			LLVMBuildStore(codegen.builder, LLVMCodegenGetValue(codegen, val), gep)
		case CallIntrinsic(intrin, args)
			match LLVMCodegenValues(codegen, args)
			case Args(argCount, argArray)
				match intrin
				case Arith(op)
					match op
					case Plus
						instr.llvmRef = LLVMBuildAdd(codegen.builder, argArray[0], argArray[1], "")
					case Minus
						instr.llvmRef = LLVMBuildSub(codegen.builder, argArray[0], argArray[1], "")
					case Divide
						instr.llvmRef = LLVMBuildSDiv(codegen.builder, argArray[0], argArray[1], "")
					case Multiply
						instr.llvmRef = LLVMBuildMul(codegen.builder, argArray[0], argArray[1], "")
					end
				case NewArray(elTy)
					var llvmElTy = LLVMCodegenType(codegen, GetDbgLoc(instr.dbg), elTy)
					argArray[0] = LLVMBuildMul(codegen.builder, argArray[0], LLVMConstTrunc(LLVMSizeOf(llvmElTy), llvmI32Type), "")
					var mem = LLVMBuildCall(codegen.builder, LLVMCodegenGetIntrinsic(codegen, IntrinsicAlloc), argArray, argCount, "")
					instr.llvmRef = LLVMBuildBitCast(codegen.builder, mem, LLVMPointerType(llvmElTy, 0), "")
				case StringConcat
					instr.llvmRef = LLVMBuildCall(codegen.builder, LLVMCodegenGetIntrinsic(codegen, IntrinsicStringConcat), argArray, argCount, "")
				case CompareBool(cmp)
					instr.llvmRef = LLVMBuildICmp(codegen.builder, LLVMCodegenPredicate(codegen, cmp), argArray[0], argArray[1], "")
				case CompareI32(cmp)
					instr.llvmRef = LLVMBuildICmp(codegen.builder, LLVMCodegenPredicate(codegen, cmp), argArray[0], argArray[1], "")
				case ComparePointer(cmp)
					var diff = LLVMBuildPtrDiff(codegen.builder, argArray[0], argArray[1], "")
					instr.llvmRef = LLVMBuildICmp(codegen.builder, LLVMCodegenPredicate(codegen, cmp), diff, LLVMConstInt_wrap(LLVMTypeOf(diff), 0, true), "")
				case CompareRefEq(cmp)
					var diff = LLVMBuildPtrDiff(
						codegen.builder, 
						LLVMBuildBitCast(codegen.builder, argArray[0], llvmPtrType, ""), 
						LLVMBuildBitCast(codegen.builder, argArray[1], llvmPtrType, ""),
						""
					)
					instr.llvmRef = LLVMBuildICmp(codegen.builder, LLVMCodegenPredicate(codegen, cmp), diff, LLVMConstInt_wrap(LLVMTypeOf(diff), 0, true), "")
				case CompareString(cmp)
					var diff = LLVMBuildCall(codegen.builder, LLVMCodegenGetIntrinsic(codegen, IntrinsicStringCmp), argArray, argCount, "")
					instr.llvmRef = LLVMBuildICmp(codegen.builder, LLVMCodegenPredicate(codegen, cmp), diff, LLVMConstInt_wrap(LLVMTypeOf(diff), 0, true), "")
				end
			end
		case ReadADTField(ptr, field_)
			LLVMCodegenADTDeclFull(codegen, field_.owner)
			instr.llvmRef = LLVMBuildLoad(
				codegen.builder,
				LLVMBuildStructGEP(codegen.builder, LLVMCodegenGetValue(codegen, ptr), 1 + field_.index, ""), 
				""
			)
		case WriteADTField(ptr, field_, val)
			LLVMCodegenADTDeclFull(codegen, field_.owner)
			LLVMBuildStore(
				codegen.builder, 
				LLVMCodegenGetValue(codegen, val), 
				LLVMBuildStructGEP(codegen.builder, LLVMCodegenGetValue(codegen, ptr), 1 + field_.index, "")
			)
		case CallConstructor(constr, args)
			var llvmFun = LLVMCodegenConsFunctionDecl(codegen, constr)
			
			match LLVMCodegenValues(codegen, args)
			case Args(argCount, argArray)
				instr.llvmRef = LLVMBuildCall(codegen.builder, llvmFun, argArray, argCount, "")
			end
		case CallFunction(fun, args)
			var llvmFun = LLVMCodegenFunctionDecl(codegen, fun)
			
			match LLVMCodegenValues(codegen, args)
			case Args(argCount, argArray)
				instr.llvmRef = LLVMBuildCall(codegen.builder, llvmFun, argArray, argCount, "")
			end
		case ADTExtract(val, cons, param)
			var adtPtr = LLVMCodegenGetValue(codegen, val)
			LLVMCodegenADTDeclFull(codegen, cons.owner)
			
			var idx = count_fields(cons.owner.fields) + 1 + find_param_index(cons.params, param)
			var cast = LLVMBuildBitCast(codegen.builder, adtPtr, LLVMPointerType(LLVMCodegenADTConsDecl(codegen, cons), 0), "")
			var gep = LLVMBuildStructGEP(codegen.builder, cast, idx, "")
			instr.llvmRef = LLVMBuildLoad(codegen.builder, gep, "")
		case ADTJump(val, adt_, branches, def)
			var adtPtr = LLVMCodegenGetValue(codegen, val)
			LLVMCodegenADTDeclFull(codegen, adt_)
			var count = count_branches(branches)
			var switch = LLVMBuildSwitch(codegen.builder, LLVMBuildLoad(codegen.builder, LLVMBuildStructGEP(codegen.builder, adtPtr, 0, ""), ""), def.llvmRef, count)
			
			var branchList = branches
			var i = 0
			while i < count do
				match branchList
				case Cons(cons, block, tail)
					LLVMAddCase(switch, LLVMConstInt_wrap(llvmI32Type, cons.index, true), block.llvmRef)
					branchList = tail
				end
				i = i + 1
			end
			return
		end
		
		i = i + 1
	end
	
	if LLVMGetBasicBlockTerminator(bb.llvmRef) == Null() then
		LLVMBuildUnreachable(codegen.builder)
	end
end

function LLVMCodegenValues(codegen:LLVMCodegen, args:ValueRefList):Args
	var argCount = count_values(args)
	var argArray = new pointer[argCount]
	var argList = args
	
	var i = 0
	while i < argCount do
		match argList
		case Cons(arg, tail)
			argArray[i] = LLVMCodegenGetValue(codegen, arg)
			argList = tail
		end
		i = i + 1
	end
	
	return Args.Args(argCount, argArray)
end

function LLVMCodegenGetArgumentValue(codegen:LLVMCodegen, param:IRParam):pointer
	var params = codegen.params
	while true do
		match params
		case Nil LLVMCodegenError(codegen, param.loc, "invalid codegen: no value for param")
		case Cons(param1, value, tail)
			params = tail
			if param1 == param then
				return value
			end
		end
	end
end

function LLVMCodegenGetVariableStorage(codegen:LLVMCodegen, var_:Variable):pointer
	var vars = codegen.vars
	while true do
		match vars
		case Nil LLVMCodegenError(codegen, GetDbgLoc(var_.dbg), "invalid codegen: no value for variable")
		case Cons(var_1, value, tail)
			vars = tail
			if var_1 == var_ then
				return value
			end
		end
	end
end


function LLVMCodegenBakeArguments(codegen:LLVMCodegen, frame:FunctionFrame)
	var vars = frame.vars
	while true do
		match vars
		case Nil return
		case Cons(var_, tail)
			vars = tail
			match var_
			case Parameter(param)
				var arg = LLVMCodegenGetArgumentValue(codegen, param)
				var alloca = LLVMCodegenGetVariableStorage(codegen, var_)
				LLVMBuildStore(codegen.builder, arg, alloca)
			end			
		end
	end
	
end


function LLVMCodegenVariables(codegen:LLVMCodegen, frame:FunctionFrame)
	var vars = frame.vars
	while true do
		match vars
		case Nil return
		case Cons(var_, tail)
			vars = tail
			var varTy = LLVMCodegenType(codegen, GetDbgLoc(var_.dbg), var_.type)
			var alloca = LLVMBuildAlloca(codegen.builder, varTy, GetVariableName(var_))

			codegen.vars = VarToValue.Cons(var_, alloca, codegen.vars)
		end
	end	
end


